clear;

///////////////////////////////////////////////////////////////
////// Computing Example 1.3 (simple model) of the paper. 
////// Sumin Leem, 2024
///////////////////////////////////////////////////////////////
q:=5; Fq:=FiniteField(q);
A<t>:=PolynomialRing(Fq);
Ay<x>:=PolynomialRing(A);


// Initial defining polynomial f
f:=x^3 + (2*t^2 + t)*x^2 + (t^4 + 2*t)*x + 3*t^3 + t^2 + t + 1;
"f : ";
f;
F<theta>:=FunctionField(f);
OF:=MaximalOrderFinite(F);
inf:=InfinitePlaces(F);
"Ramification indices of the infinite places of F: ", [RamificationIndex(inf[i]): i in [1..#inf]];


// Checking all places above t are not ramified
"Factorization of tOF: ", Factorization((t)*OF);

// Computing a new defining polynomial which is a simple model for F
Cf:=Maximum([Ceiling(Degree(Coefficients(f)[i])/(Degree(f)+1-i)) : i in [1..#Coefficients(f)-1]]);
Cof:=Coefficients(f);
Cofx:=[Coefficients(Cof[i]): i in [1..#Cof]];
for co in [1..#Cofx] do
    if Cofx[co] eq [] then
        Cofx[co]:=[0];
    end if;
end for;
temp:=Degree(f)*Cf;

f_simple:=Ay.1^(#Cof-1) + &+[ &+ [Cofx[i][j]*A.1^(temp-(i-1)*Cf+1-j): j in [1..#Cofx[i]]] *Ay.1^(i-1): i in [1..#Cof-1]];
"**** New defining polynomial (simple model): ";
f_simple;

// Check the ramification indices of infinte places of F with f_simple
F<theta>:=FunctionField(f_simple);
OF:=MaximalOrderFinite(F);
inf:=InfinitePlaces(F);
"Ramification indices of the infinite places of F: ", [RamificationIndex(inf[i]): i in [1..#inf]];



