clear;

///////////////////////////////////////////////////////////////
////// Computing Example 2.4 (B-representation) of the paper. 
////// Sumin Leem, 2024
///////////////////////////////////////////////////////////////
////Set up the function field found in the previous example:
q:=5; Fq:=FiniteField(q);
A<t> := PolynomialRing(Fq);
Ax<x> := PolynomialRing(A);
f:=x^3 + (t + 2)*x^2 + (2*t^3 + 1)*x + t^6 + t^5 + t^4 + 3*t^3;
F<theta> := FunctionField(f);

OF := MaximalOrderFinite(F);
OF_inf := MaximalOrderInfinite(F);
g:=Genus(F);

////Set the reference divisor B as (t)_oo
B := PoleDivisor(PrincipalDivisor(F!t));
b := Degree(B);

///Define places
Pinf:=InfinitePlaces(F);
p1:=ideal<OF|t + 2, theta +4>;
p2:=ideal<OF|t + 2, theta^2 + theta + 1>;
p3:=ideal<OF|t + 3, theta + 2>;
p4:=ideal<OF|t^4 + 3, theta + 2*t^3 + 2*t^2 + 2*t + 1>;

P1 := Place(p1);
P2 := Place(p2);
P3 := Place(p3);
P4 := Place(p4);

E1:=-2*P1+P3;

r := Floor((Degree(E1)-(g-1)-1)/b);
D1:=E1-r*B;
OMD1 := OMDivisor(D1);
BD1:=SemiReducedBasis(OMD1);

lengthBD1:= [-1*Minimum([Valuation(BD1[i], Pinf[j])+Valuation(D1, Pinf[j]): j in [1..#Pinf]]): i in [1..#BD1]];
ind:=Index(lengthBD1, Minimum(lengthBD1[1]));
a := BD1[ind];
Div_a:=Divisor(a);
D1:=D1+Div_a;
BD1:=[BD1[i]/a:  i in [1..#BD1]];

"======Example 2.4 (B-representation)";
"For E1 = -2P1 + P3, we have the following: ";
"r =     ", r;
"d =     ", Degree(D1);
"I_inf =     ", &*[Ideal(Pinf[i])^(-1*Valuation(InfiniteDivisor(D1), Pinf[i])): i in [1..#Pinf]];
"B_D1 =     " , BD1;

